#!/usr/bin/perl
#
# Generates the VCL file from accept-language.c
# depending on your language preferences
#

use strict;
use warnings;
use File::Slurp ();

use constant VCL_EPILOGUE => q{

/* Get Accept-Language header from client */
incoming_header = VRT_GetHdr(sp, HDR_REQ, "\020Accept-Language:");
/* syslog(LOG_INFO, "accept-language.vcl: incoming header \"%s\"", incoming_header); */

/* Normalize and filter out by list of supported languages */
select_language(incoming_header, lang);

/* Set another header back, so it can be consumed */
VRT_SetHdr(sp, HDR_REQ, "\032X-Varnish-Accept-Language:", lang);
/* syslog(LOG_INFO, "accept-language.vcl: selected lang   \"%s\"", lang); */

};

my @languages = @ARGV;

if (! @languages) {
    die "Usage; $0 <default_language> <list-of-languages>\n" .
        "example: $0 en bg cs de en fr it ...\n";
}

my $delim = ":";

my $default_lang = shift @languages;

# Add default language to the list if there isn't already
if (! grep { $_ eq $default_lang } @languages ) {
    unshift @languages, $default_lang;
}

# Build up list for easy matching
my $lang_list = join($delim, @languages);
$lang_list = ":$lang_list:";

my $c_code = File::Slurp::read_file('accept-language.c');
my @c_code = split "\n", $c_code;
my $in_main = 0;
my @vcl_code;

for my $line (@c_code) {

    # Includes must go in the main vcl file
    next if $line =~ m{^ \s* \#include}mx;

    # Change the supported languages
    if ($line =~ m{^ \s* \#define \s+ SUPPORTED_LANGUAGES}mx) {
        push @vcl_code, qq(#define SUPPORTED_LANGUAGES "$lang_list"\n);
        next;
    }

    if ($line =~ m{^ \s* \#define \s+ DEFAULT_LANGUAGE}mx) {
        push @vcl_code, qq{#define DEFAULT_LANGUAGE "$default_lang"\n};
        next;
    }

    # Install the VCL epilogue. This is the only code that will get executed
    # that will in turn use the defined functions.
    if ($in_main) {
        if ($line =~ m/^ } \s* $/mx) {
            $in_main = 0;
            push @vcl_code, VCL_EPILOGUE;
        }
        next;
    }
    elsif($line =~ m{^ \s* int \s+ main \s* \(}mx) {
        $in_main = 1;
        next;
    }

    push @vcl_code, "$line\n";
}

@vcl_code = (
    "C{\n\n",
    "/* ------------------------------------------------------" . ("-" x length($0)) . " */\n",
    "/* THIS FILE IS AUTOMATICALLY GENERATED BY $0. DO NOT EDIT. */\n\n",
    @vcl_code,
    "\n/* THIS FILE IS AUTOMATICALLY GENERATED BY $0. DO NOT EDIT. */\n",
    "/* ------------------------------------------------------" . ("-" x length($0)) . " */\n",
    "}C\n",
);

print @vcl_code;

