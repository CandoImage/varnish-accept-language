#!/usr/bin/perl
#
# Generates the VCL file from accept-language.c
# depending on your language preferences
#

use strict;
use warnings;

my @languages = @ARGV;

if (! @languages) {
    die "Usage; $0 <default_language> <list-of-languages>\n" .
        "example: $0 en de en es fr it no ru zh-cn ...\n";
}

my $delim = ":";
my $default_lang = shift @languages;

# Add default language to the list if there isn't already
if (! grep { $_ eq $default_lang } @languages ) {
    unshift @languages, $default_lang;
}

# Build up list for easy matching
my $lang_list = join($delim, @languages);
$lang_list = ":$lang_list:";

my @c_code = <STDIN>;
my @vcl_code;

READ_C: while (my $line = shift @c_code) {

    # Parse VCL ifdefs
    if ($line =~ m{^ \s* \#(ifn?def) \s+ __VCL__}mx) {
        #warn "\$1 = $1   \$line = $line\n";
        my $include = $1 eq 'ifdef' ? 1 : 0;
        IFDEF: while ($line = shift @c_code) {
            #warn "\tread_line $line\n";
            if ($line =~ m{^ \s* \#endif}mx) {
                last IFDEF;
            }
            elsif ($line =~ m{^ \s* \#else}mx) { 
                $include = 1 - $include;
            }
            push @vcl_code, $line if $include;
        }
        next;
    }

    # Change the supported languages
    if ($line =~ m{^ \s* \#define \s+ SUPPORTED_LANGUAGES}mx) {
        push @vcl_code, qq{#define SUPPORTED_LANGUAGES "$lang_list"\n};
        next;
    }

    if ($line =~ m{^ \s* \#define \s+ DEFAULT_LANGUAGE}mx) {
        push @vcl_code, qq{#define DEFAULT_LANGUAGE "$default_lang"\n};
        next;
    }

    push @vcl_code, $line;
}

@vcl_code = (
    "C{\n\n",
    "/* ------------------------------------------------------" . ("-" x length($0)) . " */\n",
    "/* THIS FILE IS AUTOMATICALLY GENERATED BY $0. DO NOT EDIT. */\n\n",
    @vcl_code,
    "\n/* THIS FILE IS AUTOMATICALLY GENERATED BY $0. DO NOT EDIT. */\n",
    "/* ------------------------------------------------------" . ("-" x length($0)) . " */\n",
    "}C\n",
);

print @vcl_code;

